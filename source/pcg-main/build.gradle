plugins {
	id 'application'
}

application {
    mainClass = 'com.palmyralabs.pcg.main.CodeGenMain'

    startScripts {
        applicationName = 'codeGen'
        classpath += files('conf')

        doLast {
            def windowsScriptFile = file getWindowsScript()
            def unixScriptFile    = file getUnixScript()
            windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\\lib\\conf', '%APP_HOME%\\resources')
            unixScriptFile.text  = unixScriptFile.text.replace('$APP_HOME/lib/conf', '$APP_HOME/resources')
        }
    }
}


dependencies {
	implementation project(':pcg-commons')
    implementation project(':pcg-generator')
    implementation project(':pcg-inputvalidator')
    implementation project(':pcg-reactgen')
    implementation project(':pcg-templategen')
    implementation project(':pcg-springgen')
    implementation project(':pcg-sqlmetareader')
    
    implementation deps.palmyra.store.schema
    implementation deps.palmyra.store.commons
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.7.0'
}



distZip {
   exclude('**/*.yaml')
   exclude('**/*.xml')
}

jar {
   exclude('**/*.yaml')
   exclude('**/*.xml')
}
	
distributions {
    main {        
        contents {            
            from ('src/main/resources'){
                exclude('application.yaml')
                into 'resources'
            }

            from 'src/main/resources/application.yaml'
        }
    }
}

installDist {
	into ('../dist')
}

applicationDistribution.with {
    include 'application.yaml'
}